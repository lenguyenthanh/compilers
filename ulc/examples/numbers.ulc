-- true
T = λx. λy. x
-- false
F = λx. λy. y

-- not
! = λb. b F T
&& = λa. λb. a b F
|| = λa. λb. a T b

if = λb. λx. λy. b x y

-- pair
pair = λf. λs. λb. b f s
fst = λp. p T
snd = λp. p F

-- numbers
0 = λs. λz. z
1 = λs. λz. s z
2 = λs. λz. s (s z)

-- numbers operations
-- successor number
succ = λn. λs. λz. s (n s z)
+ = λa. λb. λs. λz. a s (b s z)
* = λm. λn. m (+ n) 0
^ = λm. λn. n (* m) 1

is0 = λn. n (λx. F) T
is1 = λn. eqN n 1

-- predeccessor
zz = pair 0 0
ss = λp. pair (snd p) (+ 1 (snd p))
prd = λm. fst (m ss zz)

- = λm. λn. n prd m

eqN = λm. λn. && (is0 (- m n)) (is0 (- n m))

Y = λf.(λx.f(λy.(x x)y))(λx.f(λy.(x x)y))
fact' = λr. λn. (is0 n) 1 (* n (r (prd n)))
fact = λn. Y fact' n

2 = + 1 1
3 = + 2 1
4 = * 2 2
16 = ^ 2 4
